# Compare different ways to construct binary trees with parent pointers

query {
  ltree(x1,x2,x3) |= grtree(x2,x3,x1)
}
sid {
  # construct a binary tree with parent pointers starting at the leftmost leaf.
  # x1 = leftmost leaf
  # x2 = root
  # x3 = parent of root
  ltree <= x1 -> (nil,nil,p) * lltree(p,x1,x2,x3);
  lltree <= x1 -> (x2, r, p) * tree(r,x1) * lltree(p,x1,x3,x4);
  lltree <= x1 -> (x2, r, x3) * tree(r,x1) * lroot(x3,x1,x4);
  lroot <= x1 -> (x2,r,x3) * tree(r,x1);
  #
  # standard construction of a binary tree with parent pointers in which we additionally
  # keep track of the leftmost leaf
  # x1 = root
  # x2 = parent 
  # x3 = leftmost leaf
  grtree <= x1 -> (l,r,x2) * rtree(l,x1,x3) * tree(r,x1);
  rtree <= x1 -> (x3,r,x2) * parent(x3,x1) * tree(r,x1);
  rtree <= x1 -> (l,r,x2) * rtree(l,x1,x3) * tree(r,x1);
  # auxiliaries
  tree <= x1 -> (nil, nil, x2) ;
  tree <= x1 -> (y, z, x2) * tree(y, x1) * tree(z, x1) ;
  parent <= x1 -> (nil,nil,x2);
}
info {
  status = true
}
